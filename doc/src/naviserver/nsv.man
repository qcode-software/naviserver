[include version_include.man]
[manpage_begin nsv n [vset version]]
[moddesc {NaviServer Built-in Commands}]

[titledesc {NaviServer nsv Commands}]

[description]
The nsv commands provide a high performance data sharing mechanism.
This facility is much flexible alternative to the obsolete ns_share
command.  The model uses an array syntax and includes more features.
In addition, lock contention is managed in a much more scalable
way--something that is not possible with the obsolete ns_share
facility.


[section {COMMANDS}]

[list_begin definitions]


[call [cmd nsv_array] [arg get] [arg array] [opt [arg pattern]]]

[call [cmd nsv_array] [arg set] [arg array] [arg value-list]]

[call [cmd nsv_array] [arg reset] [arg array] [arg value-list]]

[call [cmd nsv_array] [arg exists] [arg array]]

[call [cmd nsv_array] [arg size] [arg array]]

[call [cmd nsv_array] [arg names] [arg array] [opt [arg pattern]]]

Commands for the most part mirror the cooresponding Tcl command for
ordinary variables.


[example_begin]
   % nsv_array set shared_array { key1 value1 key2 value2 }

   % nsv_array get shared_array
   key1 value1 key2 value2
   % nsv_array reset shared_array { key3 value3 }
   % nsv_array exists shared_array
   1
   % nsv_array size shared_array
   1
   % nsv_array names shared_array
   key3
   % nsv_array set shared_array [lb]array get tmp_shared_array[rb]
   % array set tmp_shared_array [lb]nsv_array get shared_array[rb]
   % nsv_array reset shared_array [lb]array get tmp_shared_array[rb]
[example_end]


[call [cmd nsv_bucket] [opt [arg bucket-nr]]]

Return a list of all the array names with lock counts from the specified
bucket. If no bucket-nr is specified, return a list of all arrays from
all buckets.  This command is mainly for performance tuning. When e.g.
the number of locks for a certain bucket is high one can use this
command to determine the arrays with their usages from this bucket.

[example_begin]
   set buckets ""
   set count -1
   foreach b [lb]nsv_bucket[rb] {
      append buckets "[lb]incr count[rb]: " [lb]lsort -integer -index 1 -decreasing $b[rb] \n
   }
   ns_log notice $buckets
[example_end]




[call [cmd nsv_exists] [arg array] [arg key]]

Test whether a key exists in the nsv array.

[example_begin]
   % nsv_exists shared_array key1
   1
   % nsv_exists shared_array key2
   0
[example_end]


[call [cmd nsv_get] [arg array] [arg key]]


Get the value for the key from the nsv array.  Error occurs if key does not exist.

[example_begin]
   % nsv_get shared_array key1
   value1
[example_end]


[call [cmd nsv_incr] [arg arrayName] [arg key] [opt [arg increment]]]


If increment is supplied then its value (which must be an integer) is added to the
value of the element key; otherwise 1 is added to the value of the element key.
Unlike the TCL equivalent if key does not exists it is created. Returns the new value
of the element specified by key. Internally interlocked so it is thread safe, no mutex required.


[example_begin]
   % nsv_incr shared_array foo
   1
   % nsv_incr shared_array foo -1
   0
[example_end]

[call [cmd nsv_append] [arg array] [arg key] [arg value] [opt [arg {value ...}]]]

Append all of the value arguments to the current value of variable key in the array.
If key doesn't exist, it is given a value equal to the concatenation of all the value arguments

[example_begin]
   % nsv_append shared_array key1 foo
   value1foo
[example_end]


[call [cmd nsv_lappend] [arg array] [arg key] [arg value] [opt [arg {value ...}]]]


Append all of the value arguments as list elements to variable key in the array.
If key doesn't exist, it is created as a list with elements given by the value arguments


[example_begin]
   % nsv_lappend shared_array key1 value2
   value1 value2
[example_end]


[call [cmd nsv_names] [opt [arg pattern]]]

Return a list of all the nsvs in use, optionally only those matching pattern. If no
matching nsvs are in use returns the empty string.

[example_begin]
   % nsv_names
   shared_array
[example_end]


[call [cmd nsv_set] [arg array] [arg key] [arg value]]


Set the value for a key in an nsv array. Returns the value the key is set to.

[example_begin]
   % nsv_set shared_array key1 value1
   value1
[example_end]


[call [cmd nsv_unset] [opt [arg -nocomplain]] [opt --] [arg array] [opt [arg key]]]

Unset an array or a single key from an array. If successful returns an
empty string. If -nocomplain is specified as the first argument, the
command does not complain when the specified variable does not exist.

[example_begin]
   % nsv_unset shared_array key1
   % nsv_unset shared_array
[example_end]


[list_end]

[section {Migrating From ns_share}]

Migrating from ns_share is straightforward.  If your init.tcl included commands such as:

[example_begin]
	ns_share myshare
	set myshare(lock) [lb]ns_mutex create[rb]

[example_end]

use instead:

[example_begin]

	nsv_set myshare lock [lb]ns_mutex create[rb]

[example_end]


In your procedures, instead of:

[example_begin]

	proc myproc {} {
		ns_share myshare
		ns_mutex lock $myshare(lock)
		...


[example_end]

use:

[example_begin]

	proc myproc {} {
		ns_mutex lock [lb]nsv_get myshare lock[rb]
		...


[example_end]

and within an ADP page, instead of:


[example_begin]
	<%
	ns_share myshare
	ns_puts $myshare(key1)
	%>

	<%=$myshare(key2)%>


[example_end]

use:


[example_begin]
	<%
	ns_puts [lb]nsv_get myshare key1[rb]
	%>

	<%=[lb]nsv_get myshare key2[rb]%>


[example_end]

Notice that, unlike ns_share, no command is required to define the
shared array.  The first attempt at setting the variable through any
means will automaticaly create the array.  Also notice that only arrays
are supported.  However, to migrate from ns_share you can simply package
up all existing ns_share scalars into a single array with a short name,
perhaps just ".". For example, if you had:


[example_begin]
	ns_share mylock myfile
	set myfile /tmp/some.file
	set mylock [lb]ns_mutex create[rb]

[example_end]


you can use:

[example_begin]

	nsv_set . myfile /tmp/some.file
	nsv_set . mylock [lb]ns_mutex create[rb]


[example_end]


[section {Multithreading Features}]

One advantages of nsv is built in interlocking for thread safety.
For example, consider a case of a "increment-by-one" unique id system.
Here's the ns_share solution:


[example_begin]
	ns_share ids
	set ids(lock) [lb]ns_mutex create[rb]
	set ids(next) 0

	proc nextid {} {
		ns_share ids
		ns_mutex lock $ids(lock)
		set next [lb]incr ids(next)[rb]
		ns_mutex unlock $ids(lock)
		return $next
	}


[example_end]


and here's an nsv solution:


[example_begin]

	nsv_set ids next 0

	proc nextid {} {
		return [lb]nsv_incr ids next[rb]
	}


[example_end]


Note that the nsv solution does not need a mutex as the nsv_incr command
is internally interlocked.


[section {Compatibility with Tcl Arrays}]

Another useful feature of nsv is the nsv_array command which works much
like the Tcl array command.  This can be used to import and export values
from ordinary Tcl arrays.  For example, to copy from Tcl use:


[example_begin]

	nsv_array set meta [lb]array get tmpmeta[rb]

[example_end]


and to copy to Tcl use:

[example_begin]


	array set metacopy [lb]nsv_array get meta[rb]


[example_end]


As with all other nsv command, nsv_array is atomic and no explicit
locking is required.  This feature can be used to contruct a new nsv
array by first filling up an ordinary temporary Tcl array via some time
consuming process and then swapping it into place as above.  While the
new temporary array is being constructed, other threads can access the
old array without delay or inconsistant data.  You can even reset a
complete nsv array in one step with "reset".  For example, instead of:


[example_begin]

	ns_share lock meta
	set lock [lb]ns_mutex create[rb]
	ns_mutex lock $lock
	unset meta
	array set meta [lb]array get tmpmeta[rb]
	ns_mutex unlock $lock

[example_end]


you can simply use:


[example_begin]


	nsv_array reset meta [lb]array get tmpmeta[rb]


[example_end]


The reset option will flush and then reset all values atomically,
eliminating the need for the explicit lock.


[section {Configuration}]


The nsv system uses a common multithreading technique to reduce the
potential for lock contention which is to split the locks to acheive
finer grained locking.  This technique groups arrays randomly into
buckets and only the arrays within a particular bucket share a lock.
The number of buckets to be used can be configured by setting the
"nsvbuckets" tcl parameters, e.g.:

[example_begin]


	[lb]ns/server/server1/tcl[rb]
        nsvbuckets=20

[example_end]


The default is 8 which should be reasonalbe.  Note that you can monitor
the lock contention, if any, by enabling mutex metering:


[example_begin]

	[lb]ns/threads[rb]
        mutexmetering=on

[example_end]

and then viewing the results of "ns_info locks" command after the server
has been running for some time.  The nsv locks all have names of the
form "nsv:##".  If you find many lock attempts which did not successed
immediately, try increasing nsvbuckets.



[see_also nsd]


[manpage_end]

